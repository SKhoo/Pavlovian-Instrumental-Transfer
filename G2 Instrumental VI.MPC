\-------------------------------------------------------------------------
\ G2 Instrumental VI
\ Version: 1
\ Created by Shaun Khoo (22 June 2019) based on S1 Operant Self-Administration
\ >('.')<
\ http://orcid.org/0000-0002-0972-3788
\ License: MIT
\-------------------------------------------------------------------------

\CONTROL CONSTANTS
^SessionLength = 20	\Session length in minutes
^DermanVI = 10		\Use a VI schedule from Derman and Ferrario (2018; doi: 10.1016/j.neuropharm.2017.12.039)
			\Can be set to 10, 30 or 60
			\Set to 0 for a Fleshler and Hoffman (1962; doi: 10.1901%2Fjeab.1962.5-529) distribution
^FleshlerHoffmanVI = 0	\Will initialise a VI schedule according to Fleshler and Hoffman (1962).
			\Set to 0 to use a constrained uniform distribution selected by Derman and Ferrario (2018).			
^MaximumRewards = 0	\Maximum rewards, set to 0 for no maximum
^TimebinSize = 1	\Timebin size in minutes
^ActiveLever = 1	\Active lever, set to 1 for left, 10 for right
^RewardIdentity = 1	\1 = reward delivered via fluid dispenser
			\2 = reward delivered pellet dispenser
^Version = 100		\Program version number

\INPUTS
^LeftLever = 1
^RightLever = 3
^Magazine = 4
^ActivityOne = 5
^ActivityTwo = 6
^ActivityThree = 7
^ActivityFour = 8

\OUTPUTS
^LeftOut = 1
^RightOut = 10
^LiquidDispenser = 2

^Pellet = 3
^LeftLight = 4
^RightLight = 5
^Tone = 6
^Houselight = 7
^Pump = 8
^Fan = 9
\^Clicker = ??
	
\Z-PULSES
^Reward = 1
^EndTimeout = 2
^Shutdown = 3

\VARIABLES
\VARIABLES - DATA - Session Totals
DIM A = 25
\Active Lever Presses = A(0)
\Inactive Lever Presses = A(1)
\Rewards = A(2)
\Magazine Entries = A(3)
\Total Activity = A(4)
\Timeout Active Lever = A(5)
\Timeout Inactive Lever = A(6)
\Timeout Magazine Entries = A(7)
\Timeout Total Activity = A(8)
\Active Lever Latency = A(9)
\Inactive Lever Latency = A(10)
\Reward Latency = A(11)
\Magazine Entry Latency = A(12)
\ActivityOne Total = A(13)
\ActivityTwo Total = A(14)
\ActivityThree Total = A(15)
\ActivityFour Total = A(16)
\ActivityOne Timeout = A(17)
\ActivityTwo Timeout = A(18)
\ActivityThree Timeout = A(19)
\ActivityFour Timeout = A(20)
\ActivityOne Latency = A(21)
\ActivityTwo Latency = A(22)
\ActivityThree Latency = A(23)
\ActivityFour Latency = A(24)
var_alias Weight = A(25)

\VARIABLES - DATA - Timecourse Arrays
DIM B = 50000	\Active Lever,			subscript S/X(5)
DIM C = 50000	\Inactive Lever			subscript S/X(6)
DIM D = 50000	\Rewards			subscript S/X(7)
DIM E = 50000	\Magazine Entries		subscript S/X(8)
DIM F = 50000	\ActivityOne			subscript S/X(9)
DIM G = 50000	\ActivityTwo			subscript S/X(10)
DIM H = 50000	\ActivityThree			subscript S/X(11)
DIM I = 50000	\ActivityFour			subscript S/X(12)

\VARIABLES - DATA - Timebin Arrays
DIM J = 10000	\Active Lever,			subscript T
DIM K = 10000	\Inactive Lever,		subscript T
DIM L = 10000	\Rewards,			subscript T
DIM M = 10000	\Magazine Entries,		subscript T
DIM N = 10000	\Total Activity,		subscript T
DIM O = 10000	\ActivityOne,			subscript T
DIM P = 10000	\ActivityTwo,			subscript T
DIM Q = 10000	\ActivityThree,			subscript T
DIM R = 10000	\ActivityFour,			subscript T

\VARIABLES - CONTROL - VI LISTS
\Derman and Ferrario (2018) used Excel's RANDBETWEEN function to generate a uniform
\distribution that provides a mean VI as specified with a particular range.
\Each list's length is set to approximately match the session length

\VARIABLES - CONTROL - VI10 LIST
\This list has a mean of 10, a range of 5-15, and sums to approximately 20 min
\If using the Fleshler and Hoffman (1962) distribution, this list will be initialised
\into the appropriate values.
LIST U = 5,6,6,12,15,13,14,14,7,13,7,11,10,13,7,5,8,14,15,12,11,
12,9,9,5,9,11,11,13,12,5,6,8,11,9,10,13,12,6,12,13,6,13,14,5,10,
6,15,14,5,13,11,13,12,12,5,6,11,10,15,8,6,10,12,12,14,7,14,12,5,15,
11,5,10,15,14,14,8,15,7,12,6,11,5,7,5,15,9,9,15,11,7,7,10,14,12,
8,8,14,10,10,5,8,8,7,13,12,14,6,6,10,15,15,12,14,7,6,7,6,8

\VARIABLES - CONTROL - VI30 LIST
\This list has a mean of 30, a range of 15-45, and sums to approximately 20 min
LIST V = 25,19,28,44,43,38,30,25,29,17,37,18,44,40,31,44,44,27,23,23,36,34,33,18,19,
37,42,20,16,23,23,23,29,28,27,44,35,35,38,16

\VARIABLES - CONTROL - VI60 LIST
\This list has a mean of 60, a range of 30-90, and sums to approximately 20 min
LIST W = 73,74,79,69,32,52,51,72,50,48,88,77,61,
90,53,49,80,40,45,76

\VARIABLES - CONTROL - WORKING VARIABLES
DIM X = 12
\X(0) = Session Clock (seconds)
\X(1) = Centisecond Clock
\X(2) = Timebin Clock
\X(3) = Reward availability
\X(4) = Current Interval
\X(5) = Subscript for B
\X(6) = Subscript for C
\X(7) = Subscript for D
\X(8) = Subscript for E
\X(9) = Subscript for F
\X(10) = Subscript for G
\X(11) = Subscript for H
\X(12) = Subscript for I

\VARIABLES - CONTROL - SESSION PARAMETERS
DIM Z = 8
var_alias Session Length (min) = Z(0)
var_alias Derman VI = Z(1)
var_alias Fleshler Hoffman VI = Z(2)
var_alias Maximum Rewards = Z(3)
var_alias Number of Timebins = Z(4)
var_alias Active Lever (1L 3R) = Z(5)
var_alias Inactive Lever (1L 3R) = Z(6)
var_alias Reward Identity = Z(7)
var_alias Version = Z(8)

DISKVARS = A, B, C, D, E, F, G, H, I, J, K, L, M, N, O, P, Q, R, U, V, W, Z
DISKOPTIONS = FULLHEADERS
DISKFORMAT = 16.3
DISKCOLUMNS = 1
Y2KCOMPLIANT

\-------------------------------------------------------------------------
\State Set 1 - Control Program

S.S.1,
S1, 	\State 1 shows the program has loaded.
	0.01": SET Z(8) = ^Version / 100;
	SHOW 1, Program Version, Z(8);

	\Set the variables according to the control constants
	SET Z(0) = ^SessionLength, Z(1) = ^DermanVI, Z(2) = ^FleshlerHoffmanVI;
	SET Z(3) = ^MaximumRewards, Z(4) = Z(0) / ^TimebinSize;
	SET Z(5) = ^ActiveLever, Z(7) = ^RewardIdentity;

	\Seal all arrays
	SET B(1) = -987.987, C(1) = -987.987, D(1) = -987.987;		\Timestamp recorders
	SET E(1) = -987.987, F(1) = -987.987, G(1) = -987.987;
	SET H(1) = -987.987, I(1) = -987.987;
	SET J(1) = -987.987, K(1) = -987.987, L(1) = -987.987;		\Timebin recorders
	SET M(1) = -987.987, N(1) = -987.987, O(1) = -987.987;
	SET P(1) = -987.987, Q(1) = -987.987, R(1) = -987.987;
	SET A(25) = -987.987;						\Seal the weight element

	\Set the active lever
	IF Z(5) < 2 [@LeftActive, @RightActive]
		@LeftActive: SET Z(5) = ^LeftLever, Z(6) = ^RightLever ---> S2
		@RightActive: SET Z(5) = ^RightLever, Z(6) = ^LeftLever ---> S2

S2,	\State 2 waits for the start command
	#START: SHOW 1, Session Time, X(0); ON ^Houselight, ^Fan;

	\Set the program start time
	SET STARTHOURS = CURRENTHOURS;
	SET STARTMINUTES = CURRENTMINUTES, STARTSECONDS = CURRENTSECONDS;

	\Set default latencies
	SET B(0) = Z(0) * 60, C(0) = Z(0) * 60, D(0) = Z(0) * 60;
	SET E(0) = Z(0) * 60, F(0) = Z(0) * 60, G(0) = Z(0) * 60;
	SET H(0) = Z(0) * 60, I(0) = Z(0) * 60 ---> S3

S3,	\State 3 counts the session length in seconds
	1": ADD X(0); SHOW 1, Session Time, X(0);
	IF X(0) >= Z(0) * 60 [@SHUTDOWN, @CONTINUE]
		@SHUTDOWN: Z^Shutdown;
		SET A(9) = B(0), A(10) = C(0), A(11) = D(0);		\Transfer latency values to the A array
		SET A(12) = E(0), A(21) = F(0), A(22) = G(0);
		SET A(23) = H(0), A(24) = I(0);
		OFF ^LeftOut, ^RightOut, ^Houselight, ^Fan ---> S4
		@CONTINUE: ---> SX

S4,	\State 4 waits for 1 second and closes the program
	1": ---> STOPABORTFLUSH

\-------------------------------------------------------------------------
\State Set 2 - Centisecond Timer

S.S.2,
S1,	\State 1 waits for the start command
	#START:  ---> S2

S2,	\State 2 counts upwards in 10 ms increments
	0.01": SET X(1) = X(1) + 0.01;
	SET X(2) = X(2) + 0.01; 
	IF (X(2) / 60 >= (Z(0) / Z(4))) AND ((T + 1) < Z(4)) [@TRUE, @FALSE]
		@TRUE: SET X(2) = 0; ADD T;
		SET J(T) = 0, K(T) = 0, L(T) = 0, M(T) = 0;
		SET N(T) = 0, O(T) = 0, P(T) = 0, Q(T) = 0;
		SET R(T) = 0;
		SET J(T+1) = -987.987, K(T+1) = -987.987;
		SET L(T+1) = -987.987, M(T+1) = -987.987;
		SET N(T+1) = -987.987, O(T+1) = -987.987;
		SET P(T+1) = -987.987, Q(T+1) = -987.987;
		SET R(T+1) = -987.987 ---> SX
		@FALSE: ---> SX

	#Z^Shutdown: ---> S3

S3,	\State 3 is a holding state
	#START: ---> SX

\-------------------------------------------------------------------------
\State Set 3 - Variable Interval Timer

S.S.3,
S1,	\State 1 waits for the start command and initialises the appropriate
	\VI schedule
	#START: IF (Z(1) = 10) AND (Z(2) <= 0) [@DERMANTEN, @CHECK]
		@DERMANTEN: RANDD X(4) = U; SET X(4) = X(4) * 1" ---> S2
		@CHECK: IF (Z(1) = 30) AND (Z(2) <= 0) [@DERMANTHIRTY, @CHECK]
			@DERMANTHIRTY: RANDD X(4) = V; SET X(4) = X(4) * 1" ---> S4
			@CHECK: IF (Z(1) = 60) AND (Z(2) <= 0) [@DERMANSIXTY, @CHECK]
				@DERMANSIXTY: RANDD X(4) = W; SET X(4) = X(4) * 1" ---> S6
				@CHECK: IF Z(2) > 0 [@FLESHLERHOFFMAN, @ERROR]
					@FLESHLERHOFFMAN: INITCONSTPROBARR U, Z(2);
					RANDD X(4) = U; SET X(4) = X(4) * 1" ---> S2
					@ERROR: SHOW 1, VI SETTINGS ERROR, Z(2) ---> STOPKILL

S2,	\State 2 operates the Derman VI-10 or the Fleshler and Hoffman custom VIs.
	X(4)#T: SET X(3) = 1 ---> S3

S3,	\State 3 waits for reward delivery and selects the next interval
	#Z^EndTimeout: RANDD X(4) = U; SET X(4) = X(4) * 1" ---> S2

S4,	\State 4 operates the Derman VI-30
	X(4)#T: SET X(3) = 1 ---> S5

S5,	\State 5 waits for reward delivery and selects the next VI-30 interval
	#Z^EndTimeout: RANDD X(4) = V; SET X(4) = X(4) * 1" ---> S4
	
S6,	\State 6 operates the Derman VI-60
	X(4)#T: SET X(3) = 1 ---> S7

S7,	\State 7 waits for reward delivery and selects the next VI-60 interval
	#Z^EndTimeout: RANDD X(4) = W; SET X(4) = X(4) * 1" ---> S6

\-------------------------------------------------------------------------
\State Set 4 - Active Lever

S.S.4,
S1,	\State 1 waits for the start command
	#START: SHOW 2, Active Lever, A(0);
	SHOW 4, Rewards, A(2); ON ^LeftOut, ^RightOut ---> S2

S2,	\State 2 counts active lever presses
	#RZ(5): ADD A(0), J(T); SET S = X(5); SET B(S) = X(1);
	ADD X(5); SET S = X(5); SET B(S) = -987.987;
	SHOW 2, Active Lever, A(0);

	\If the VI has elapsed, S.S.3 will have set X(3) to 1.
	\This conditional will check if X(3) is > 0 and then deliver
	\the reward.
	IF X(3) > 0.5 [@REWARD, @NULL]
		@REWARD: Z^Reward; ADD A(2), L(T);
		SET S = X(7); SET D(S) = X(1); ADD X(7);
		SET S = X(7); SET D(S) = -987.987;
		SHOW 4, Rewards, A(2);
		SET X(3) = 0 ---> S3
		@NULL: ---> SX

	#Z^Shutdown: ---> S4

S3,	\State 3 counts active lever presses during the dispenser operation
	#RZ(5): ADD A(0), A(5), J(T), X(3); SET S = X(5); SET B(S) = X(1);
	ADD X(5); SET S = X(5); SET B(S) = -987.987;
	SHOW 2, Active Lever, A(0) ---> SX

	#Z^EndTimeout: ---> S2
	#Z^Shutdown: ---> S4

S4,	\State 4 is a holding state
	#START: ---> SX

\-------------------------------------------------------------------------
\State Set 5 - Reward and cue delivery

S.S.5,
S1,	\State 1 waits for the start command
	#START: ---> S2

S2,	\State 2 waits for the ^Reward Z-Pulse
	#Z^Reward: IF Z(7) = 1 [@FLUID, @PELLET]
		@FLUID: ON ^LiquidDispenser ---> S3
		@PELLET: ON ^Pellet ---> S3

	#Z^Shutdown: ---> S4

S3,	\State 3 waits to switch things off
	0.5": IF Z(7) = 1 [@FLUID, @PELLET]
		@FLUID: OFF ^LiquidDispenser; Z^EndTimeout ---> S2
		@PELLET: OFF ^Pellet; Z^EndTimeout ---> S2

	#Z^Shutdown: OFF ^LiquidDispenser, ^Pellet ---> S4

S4,	\State 4 is a holding state
	#START: ---> SX

\-------------------------------------------------------------------------
\State Set 6 - Inactive Lever

S.S.6,
S1,	\State 1 waits for the start command
	#START: SHOW 3, Inactive Lever, A(1) ---> S2

S2,	\State 2 counts inactive lever presses
	#RZ(6): ADD A(1), K(T); SET S = X(6); SET C(S) = X(1); ADD X(6);
	SET S = X(6); SET C(S) = -987.987; SHOW 3, Inactive Lever, A(1) ---> SX

	#Z^Reward: ---> S3
	#Z^Shutdown: ---> S4

S3,	\State 3 counts inactive lever presses during reward delivery
	#RZ(6): ADD A(1), A(6), K(T); SET S = X(6); SET C(S) = X(1); ADD X(6);
	SET S = X(6); SET C(S) = -987.987; SHOW 3, Inactive Lever, A(1) ---> SX

	#Z^EndTimeout: ---> S2
	#Z^Shutdown: ---> S4

S4,	\State 4 is a holding state
	#START: ---> SX

\-------------------------------------------------------------------------
\State Set 7 - Magazine Entries

S.S.7,
S1,	\State 1 waits for the start command
	#START: SHOW 5, Magazine Entries, A(3) ---> S2

S2,	\State 2 counts magazine entries
	#R^Magazine: ADD A(3), M(T); SET S = X(8); SET E(S) = X(1);
	ADD X(8); SET S = X(8); SET E(S) = -987.987;
	SHOW 5, Magazine Entries, A(3) ---> SX

	#Z^Reward: ---> S3
	#Z^Shutdown: ---> S4

S3,	\State 3 counts magazine entries during reward delivery
	#R^Magazine: ADD A(3), A(7), M(T); SET S = X(8); SET E(S) = X(1);
	ADD X(8); SET S = X(8); SET E(S) = -987.987;
	SHOW 5, Magazine Entries, A(3) ---> SX

	#Z^EndTimeout: ---> S2
	#Z^Shutdown: ---> S4

S4,	\State 4 is a holding state
	#START: ---> SX

\-------------------------------------------------------------------------
\State Set 8 - Locomotor Activity

S.S.8,
S1,	\State 1 waits for the start command
	#START: SHOW 6, Locomotor Counts, A(4) ---> S2

S2,	\State 2 will count any locomotor beam break
	#R^ActivityOne: ADD A(4), A(13), N(T), O(T); SET S = X(9);
	SET F(S) = X(1); ADD X(9); SET S = X(9); SET F(S) = -987.987;
	SHOW 6, Locomotor Counts, A(4) ---> S3

	#R^ActivityTwo: ADD A(4), A(14), N(T), P(T); SET S = X(10);
	SET G(S) = X(1); ADD X(10); SET S = X(10); SET G(S) = -987.987;
	SHOW 6, Locomotor Counts, A(4) ---> S4

	#R^ActivityThree: ADD A(4), A(15), N(T), Q(T); SET S = X(11);
	SET H(S) = X(1); ADD X(11); SET S = X(11); SET H(S) = -987.987;
	SHOW 6, Locomotor Counts, A(4) ---> S5

	#R^ActivityFour: ADD A(4), A(16), N(T), R(T); SET S = X(12);
	SET I(S) = X(1); ADD X(12); SET S = X(12); SET I(S) = -987.987;
	SHOW 6, Locomotor Counts, A(4) ---> S6

	#Z^Reward: ---> S7
	#Z^Shutdown: ---> S12

S3,	\State 3 will count any locomotor beam except ^ActivityOne
	#R^ActivityTwo: ADD A(4), A(14), N(T), P(T); SET S = X(10);
	SET G(S) = X(1); ADD X(10); SET S = X(10); SET G(S) = -987.987;
	SHOW 6, Locomotor Counts, A(4) ---> S4

	#R^ActivityThree: ADD A(4), A(15), N(T), Q(T); SET S = X(11);
	SET H(S) = X(1); ADD X(11); SET S = X(11); SET H(S) = -987.987;
	SHOW 6, Locomotor Counts, A(4) ---> S5

	#R^ActivityFour: ADD A(4), A(16), N(T), R(T); SET S = X(12);
	SET I(S) = X(1); ADD X(12); SET S = X(12); SET I(S) = -987.987;
	SHOW 6, Locomotor Counts, A(4) ---> S6

	#Z^Reward: ---> S8
	#Z^Shutdown: ---> S12

S4,	\State 4 will count any locomotor beam except ^ActivityTwo
	#R^ActivityOne: ADD A(4), A(13), N(T), O(T); SET S = X(9);
	SET F(S) = X(1); ADD X(9); SET S = X(9); SET F(S) = -987.987;
	SHOW 6, Locomotor Counts, A(4) ---> S3

	#R^ActivityThree: ADD A(4), A(15), N(T), Q(T); SET S = X(11);
	SET H(S) = X(1); ADD X(11); SET S = X(11); SET H(S) = -987.987;
	SHOW 6, Locomotor Counts, A(4) ---> S5

	#R^ActivityFour: ADD A(4), A(16), N(T), R(T); SET S = X(12);
	SET I(S) = X(1); ADD X(12); SET S = X(12); SET I(S) = -987.987;
	SHOW 6, Locomotor Counts, A(4) ---> S6

	#Z^Reward: ---> S9
	#Z^Shutdown: ---> S12

S5,	\State 5 will count any locomotor beam except ^ActivityThree
	#R^ActivityOne: ADD A(4), A(13), N(T), O(T); SET S = X(9);
	SET F(S) = X(1); ADD X(9); SET S = X(9); SET F(S) = -987.987;
	SHOW 6, Locomotor Counts, A(4) ---> S3

	#R^ActivityTwo: ADD A(4), A(14), N(T), P(T); SET S = X(10);
	SET G(S) = X(1); ADD X(10); SET S = X(10); SET G(S) = -987.987;
	SHOW 6, Locomotor Counts, A(4) ---> S4

	#R^ActivityFour: ADD A(4), A(16), N(T), R(T); SET S = X(12);
	SET I(S) = X(1); ADD X(12); SET S = X(12); SET I(S) = -987.987;
	SHOW 6, Locomotor Counts, A(4) ---> S6

	#Z^Reward: ---> S10
	#Z^Shutdown: ---> S12

S6,	\State 6 will count any locomotor beam except ^ActivityFour
	#R^ActivityOne: ADD A(4), A(13), N(T), O(T); SET S = X(9);
	SET F(S) = X(1); ADD X(9); SET S = X(9); SET F(S) = -987.987;
	SHOW 6, Locomotor Counts, A(4) ---> S3

	#R^ActivityTwo: ADD A(4), A(14), N(T), P(T); SET S = X(10);
	SET G(S) = X(1); ADD X(10); SET S = X(10); SET G(S) = -987.987;
	SHOW 6, Locomotor Counts, A(4) ---> S4

	#R^ActivityThree: ADD A(4), A(15), N(T), Q(T); SET S = X(11);
	SET H(S) = X(1); ADD X(11); SET S = X(11); SET H(S) = -987.987;
	SHOW 6, Locomotor Counts, A(4) ---> S5

	#Z^Reward: ---> S11
	#Z^Shutdown: ---> S12

S7,	\State 7 will count any locomotor beam during the timeout
	#R^ActivityOne: ADD A(4), A(13), A(19), N(T), O(T); SET S = X(9);
	SET F(S) = X(1); ADD X(9); SET S = X(9); SET F(S) = -987.987;
	SHOW 6, Locomotor Counts, A(4) ---> S8

	#R^ActivityTwo: ADD A(4), A(14), A(20), N(T), P(T); SET S = X(10);
	SET G(S) = X(1); ADD X(10); SET S = X(10); SET G(S) = -987.987;
	SHOW 6, Locomotor Counts, A(4) ---> S9

	#R^ActivityThree: ADD A(4), A(15), A(21), N(T), Q(T); SET S = X(11);
	SET H(S) = X(1); ADD X(11); SET S = X(11); SET H(S) = -987.987;
	SHOW 6, Locomotor Counts, A(4) ---> S10

	#R^ActivityFour: ADD A(4), A(16), A(22), N(T), R(T); SET S = X(12);
	SET I(S) = X(1); ADD X(12); SET S = X(12); SET I(S) = -987.987;
	SHOW 6, Locomotor Counts, A(4) ---> S11

	#Z^EndTimeout: ---> S2
	#Z^Shutdown: ---> S12

S8,	\State 8 will count any locomotor beam during the timeout except ^ActivityOne
	#R^ActivityTwo: ADD A(4), A(14), A(20), N(T), P(T); SET S = X(10);
	SET G(S) = X(1); ADD X(10); SET S = X(10); SET G(S) = -987.987;
	SHOW 6, Locomotor Counts, A(4) ---> S9

	#R^ActivityThree: ADD A(4), A(15), A(21), N(T), Q(T); SET S = X(11);
	SET H(S) = X(1); ADD X(11); SET S = X(11); SET H(S) = -987.987;
	SHOW 6, Locomotor Counts, A(4) ---> S10

	#R^ActivityFour: ADD A(4), A(16), A(22), N(T), R(T); SET S = X(12);
	SET I(S) = X(1); ADD X(12); SET S = X(12); SET I(S) = -987.987;
	SHOW 6, Locomotor Counts, A(4) ---> S11

	#Z^EndTimeout: ---> S3
	#Z^Shutdown: ---> S12

S9,	\State 9 will count any locomotor beam during the timeout except ^ActivityTwo
	#R^ActivityOne: ADD A(4), A(13), A(19), N(T), O(T); SET S = X(9);
	SET F(S) = X(1); ADD X(9); SET S = X(9); SET F(S) = -987.987;
	SHOW 6, Locomotor Counts, A(4) ---> S8

	#R^ActivityThree: ADD A(4), A(15), A(21), N(T), Q(T); SET S = X(11);
	SET H(S) = X(1); ADD X(11); SET S = X(11); SET H(S) = -987.987;
	SHOW 6, Locomotor Counts, A(4) ---> S10

	#R^ActivityFour: ADD A(4), A(16), A(22), N(T), R(T); SET S = X(12);
	SET I(S) = X(1); ADD X(12); SET S = X(12); SET I(S) = -987.987;
	SHOW 6, Locomotor Counts, A(4) ---> S11

	#Z^EndTimeout: ---> S4
	#Z^Shutdown: ---> S12

S10,	\State 10 will count any locomotor beam during the timeout except ^ActivityThree
	#R^ActivityOne: ADD A(4), A(13), A(19), N(T), O(T); SET S = X(9);
	SET F(S) = X(1); ADD X(9); SET S = X(9); SET F(S) = -987.987;
	SHOW 6, Locomotor Counts, A(4) ---> S8

	#R^ActivityTwo: ADD A(4), A(14), A(20), N(T), P(T); SET S = X(10);
	SET G(S) = X(1); ADD X(10); SET S = X(10); SET G(S) = -987.987;
	SHOW 6, Locomotor Counts, A(4) ---> S9

	#R^ActivityFour: ADD A(4), A(16), A(22), N(T), R(T); SET S = X(12);
	SET I(S) = X(1); ADD X(12); SET S = X(12); SET I(S) = -987.987;
	SHOW 6, Locomotor Counts, A(4) ---> S11

	#Z^EndTimeout: ---> S5
	#Z^Shutdown: ---> S12

S11,	\State 11 will count any locomotor beam except ^ActivityFour
	#R^ActivityOne: ADD A(4), A(13), A(19), N(T), O(T); SET S = X(9);
	SET F(S) = X(1); ADD X(9); SET S = X(9); SET F(S) = -987.987;
	SHOW 6, Locomotor Counts, A(4) ---> S8

	#R^ActivityTwo: ADD A(4), A(14), A(20), N(T), P(T); SET S = X(10);
	SET G(S) = X(1); ADD X(10); SET S = X(10); SET G(S) = -987.987;
	SHOW 6, Locomotor Counts, A(4) ---> S9

	#R^ActivityThree: ADD A(4), A(15), A(21), N(T), Q(T); SET S = X(11);
	SET H(S) = X(1); ADD X(11); SET S = X(11); SET H(S) = -987.987;
	SHOW 6, Locomotor Counts, A(4) ---> S10

	#Z^EndTimeout: ---> S6
	#Z^Shutdown: ---> S12

S12,	\State 12 is a holding state
	#START: ---> SX

\-------------------------------------------------------------------------
\State Set 9 - Pre-Session Screen Updater

S.S.9,
S1,	\State updates the screen before session start
	#START: CLEAR 6,9 ---> S2

	1": IF S.S.1 <= 2 [@SHOW, @SKIP]
	@SHOW: SHOW 1, Session Length (min), Z(0);
	SHOW 2, Maximum Rewards, Z(3);
	SHOW 3, Number of Timebins, Z(4);
	SHOW 4, Active Lever (1L 3R), Z(5);
	IF Z(7) = 1 [@FLUID, @PELLET]
		@FLUID: SHOW 5, Liquid Dispenser, Z(7) ---> SX
		@PELLET: SHOW 5, Reward via Pellet, Z(7) ---> SX
	@SKIP: CLEAR 6,9 ---> S2

S2,	\State 2 is a holding state
	#START: ---> SX